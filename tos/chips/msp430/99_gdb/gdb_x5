
#display dma
define dma
printf "tsel0: %04x  tsel1: %04x  tsel2: %04x  tsel3: %04x  dctl4: %04x  dmaiv: %04x\n", \
    *((uint16_t *) 0x500), *((uint16_t *) 0x502), *((uint16_t *) 0x504), \
    *((uint16_t *) 0x506), *((uint16_t *) 0x508), *((uint16_t *) 0x50a)
printf "d0ctl: %04x  d0sa: %04x%04x  d0da: %04x%04x  d0sz: %04x\n", \
    *((uint16_t *) 0x510), *((uint16_t *) 0x514), *((uint16_t *) 0x512), \
    *((uint16_t *) 0x518), *((uint16_t *) 0x516), *((uint16_t *) 0x51a)
printf "d1ctl: %04x  d1sa: %04x%04x  d1da: %04x%04x  d1sz: %04x\n", \
    *((uint16_t *) 0x520), *((uint16_t *) 0x524), *((uint16_t *) 0x522), \
    *((uint16_t *) 0x528), *((uint16_t *) 0x526), *((uint16_t *) 0x52a)
printf "d2ctl: %04x  d2sa: %04x%04x  d2da: %04x%04x  d2sz: %04x\n", \
    *((uint16_t *) 0x530), *((uint16_t *) 0x534), *((uint16_t *) 0x532), \
    *((uint16_t *) 0x538), *((uint16_t *) 0x536), *((uint16_t *) 0x53a)
end
document dma
Display msp430 dma hardware
end

#display timers
define dt
printf "TA0:  r: %04x  ctl: %04x  ex: %04x  iv: %04x\n", \
    *((uint16_t *)0x350),*((uint16_t *)0x340),*((uint16_t *)0x360),*((uint16_t *)0x36e)
printf "  cctl0: %04x    1: %04x   2:  %04x  3: %04x  4: %04x\n", \
    *((uint16_t *)0x342),*((uint16_t *)0x344),*((uint16_t *)0x346),*((uint16_t *)0x348),*((uint16_t *)0x34a)
printf "   ccr0: %04x    1: %04x   2:  %04x  3: %04x  4: %04x\n\n", \
    *((uint16_t *)0x352),*((uint16_t *)0x354),*((uint16_t *)0x356),*((uint16_t *)0x358),*((uint16_t *)0x35a)

printf "TA1:  r: %04x  ctl: %04x  ex: %04x  iv: %04x\n", \
    *((uint16_t *)0x390),*((uint16_t *)0x380),*((uint16_t *)0x3a0),*((uint16_t *)0x3ae)
printf "  cctl0: %04x    1: %04x   2: %04x\n", \
    *((uint16_t *)0x382),*((uint16_t *)0x384),*((uint16_t *)0x386)
printf "   ccr0: %04x    1: %04x   2: %04x\n\n", \
    *((uint16_t *)0x392),*((uint16_t *)0x394),*((uint16_t *)0x396)

#printf "TB0:  r: %04x  ctl: %04x  ex: %04x  iv: %04x\n", \
#    *((uint16_t *)0x3d0),*((uint16_t *)0x3c0),*((uint16_t *)0x3e0),*((uint16_t *)0x3ee)
#printf "    cctl0: %04x  1: %04x  2: %04x  3: %04x\n", \
#    *((uint16_t *)0x3c2),*((uint16_t *)0x3c4),*((uint16_t *)0x3c6),*((uint16_t *)0x3c8)
#printf "     ccr0: %04x  1: %04x  2: %04x  3: %04x\n", \
#    *((uint16_t *)0x3d2),*((uint16_t *)0x3d4),*((uint16_t *)0x3d6),*((uint16_t *)0x3d8)
#printf "    cctl4: %04x  5: %04x  6: %04x\n", \
#    *((uint16_t *)0x3ca),*((uint16_t *)0x3cc),*((uint16_t *)0x3ce)
#printf "     ccr4: %04x  5: %04x  6: %04x\n\n", \
#    *((uint16_t *)0x3da),*((uint16_t *)0x3dc),*((uint16_t *)0x3de)

end
document dt
Display msp430 timer hardware
end

#display usci
define us
printf "A0: ie: %02x  ifg:  %02x  iv: %04x\n", \
    (*(uint8_t *)0x5dc), (*(uint8_t *)0x5dd), (*(uint8_t *)0x5de)
printf "  ctl0: %02x  ctl1: %02x  br: %02x%02x  mctl: %02x  stat: %02x  rx: %02x  tx: %02x\n",\
    (*(uint8_t *)0x5c1), (*(uint8_t *)0x5c0), (*(uint8_t *)0x5c7), (*(uint8_t *)0x5c6),\
    (*(uint8_t *)0x5c8), (*(uint8_t *)0x5ca), (*(uint8_t *)0x5cc), (*(uint8_t *)0x5ce)

printf "B0: ie: %02x  ifg:  %02x  iv: %04x\n", \
    (*(uint8_t *)0x5fc), (*(uint8_t *)0x5fd), (*(uint8_t *)0x5fe)
printf "  ctl0: %02x  ctl1: %02x  br: %02x%02x  mctl: %02x  stat: %02x  rx: %02x  tx: %02x\n",\
    (*(uint8_t *)0x5e1), (*(uint8_t *)0x5e0), (*(uint8_t *)0x5e7), (*(uint8_t *)0x5e6),\
    (*(uint8_t *)0x5e8), (*(uint8_t *)0x5ea), (*(uint8_t *)0x5ec), (*(uint8_t *)0x5ee)

#
#printf "A1: IE: %02x   IFG: %02x  IV: %04x\n", \
#    (*(uint8_t *)0x61c), (*(uint8_t *)0x61d), (*(uint8_t *)0x61e)
#printf "  ctl0: %02x  ctl1: %02x  br: %02x%02x  mctl: %02x  stat: %02x  rx: %02x  tx: %02x\n",\
#    (*(uint8_t *)0x601), (*(uint8_t *)0x600), (*(uint8_t *)0x607), (*(uint8_t *)0x606),\
#    (*(uint8_t *)0x608), (*(uint8_t *)0x60a), (*(uint8_t *)0x60c), (*(uint8_t *)0x60e)
#
#printf "B1: IE: %02x   IFG: %02x  IV: %04x\n", \
#    (*(uint8_t *)0x63c), (*(uint8_t *)0x63d), (*(uint8_t *)0x63e)
#printf "  ctl0: %02x  ctl1: %02x  br: %02x%02x  mctl: %02x  stat: %02x  rx: %02x  tx: %02x\n",\
#    (*(uint8_t *)0x621), (*(uint8_t *)0x620), (*(uint8_t *)0x627), (*(uint8_t *)0x626),\
#    (*(uint8_t *)0x628), (*(uint8_t *)0x62a), (*(uint8_t *)0x62c), (*(uint8_t *)0x62e)
#
#printf "A2: IE: %02x   IFG: %02x  IV: %04x\n", \
#    (*(uint8_t *)0x65c), (*(uint8_t *)0x65d), (*(uint8_t *)0x65e)
#printf "  ctl0: %02x  ctl1: %02x  br: %02x%02x  mctl: %02x  stat: %02x  rx: %02x  tx: %02x\n",\
#    (*(uint8_t *)0x641), (*(uint8_t *)0x640), (*(uint8_t *)0x647), (*(uint8_t *)0x646),\
#    (*(uint8_t *)0x648), (*(uint8_t *)0x64a), (*(uint8_t *)0x64c), (*(uint8_t *)0x64e)
#
#printf "B2: IE: %02x   IFG: %02x  IV: %04x\n", \
#    (*(uint8_t *)0x67c), (*(uint8_t *)0x67d), (*(uint8_t *)0x67e)
#printf "  ctl0: %02x  ctl1: %02x  br: %02x%02x  mctl: %02x  stat: %02x  rx: %02x  tx: %02x\n",\
#    (*(uint8_t *)0x661), (*(uint8_t *)0x660), (*(uint8_t *)0x667), (*(uint8_t *)0x666),\
#    (*(uint8_t *)0x668), (*(uint8_t *)0x66a), (*(uint8_t *)0x66c), (*(uint8_t *)0x66e)
#
#printf "A3: IE: %02x   IFG: %02x  IV: %04x\n", \
#    (*(uint8_t *)0x69c), (*(uint8_t *)0x69d), (*(uint8_t *)0x69e)
#printf "  ctl0: %02x  ctl1: %02x  br: %02x%02x  mctl: %02x  stat: %02x  rx: %02x  tx: %02x\n",\
#    (*(uint8_t *)0x681), (*(uint8_t *)0x680), (*(uint8_t *)0x687), (*(uint8_t *)0x686),\
#    (*(uint8_t *)0x688), (*(uint8_t *)0x68a), (*(uint8_t *)0x68c), (*(uint8_t *)0x68e)
#
#printf "B3: IE: %02x   IFG: %02x  IV: %04x\n", \
#    (*(uint8_t *)0x6bc), (*(uint8_t *)0x6bd), (*(uint8_t *)0x6be)
#printf "  ctl0: %02x  ctl1: %02x  br: %02x%02x  mctl: %02x  stat: %02x  rx: %02x  tx: %02x\n",\
#    (*(uint8_t *)0x6a1), (*(uint8_t *)0x6a0), (*(uint8_t *)0x6a7), (*(uint8_t *)0x6a6),\
#    (*(uint8_t *)0x6a8), (*(uint8_t *)0x6aa), (*(uint8_t *)0x6ac), (*(uint8_t *)0x6ae)
#
end
document us
display msp430 usci hardware
end

define sfr
printf "sfr: ie1: %04x  ifg1: %04x  rpcr: %04x\n",\
    (*(uint16_t *)0x100), (*(uint16_t *)0x102), (*(uint16_t *)0x104)
end
document sfr
display misc interrupt/fault registers
end

#display ucs registers
# p 100. MSP430 Family User's Guide
# mps430f5438, p26  has the UCS control 0-8 register addresses and offsets

define ucss
printf "ucsctl0:  %04x  %04x  %04x\n", (*(uint16_t *)0x160),(*(uint16_t *)0x162),(*(uint16_t *)0x164)
printf "ucsctl3:  %04x  %04x  %04x\n", (*(uint16_t *)0x166),(*(uint16_t *)0x168),(*(uint16_t *)0x16a)
printf "ucsctl6:  %04x  %04x  %04x\n", (*(uint16_t *)0x16c),(*(uint16_t *)0x16e),(*(uint16_t *)0x170)
end
document ucss
display msp430 5xxx unified clock system hardware (concise, short)
end

define ucs
set $_tmp = *(uint16_t *) 0x0160
printf "ucsctl0: %04x\n",	$_tmp
printf "  dco: %x",		($_tmp & 0x1F00) >> 8
printf "  mod: %x\n",		($_tmp & 0x00f8) >> 3

set $_tmp = *(uint16_t *) 0x0162
printf "ucsctl1: %04x\n",	($_tmp)
printf "  dcorsel: %x",		($_tmp & 0x0070) >> 4
printf "  dismod:  %x\n",	($_tmp & 0x0001)

set $_tmp = *(uint16_t *) 0x0164
printf "ucsctl2: %04x\n",	($_tmp)
printf "  flld: %x",		($_tmp & 0x7000) >> 8 >> 4
printf "  flln: %x\n",		($_tmp & 0x03FF)

set $_tmp = *(uint16_t *) 0x0166
printf "ucsctl3: %04x\n",	($_tmp)
printf "  selref: %x",		($_tmp & 0x0070) >> 4
printf "  fllrefdiv: %x\n",	($_tmp & 0x0007)

set $_tmp = *(uint16_t *) 0x0168
printf "ucsctl4: %04x\n",	($_tmp)
printf "  sela: %x",		($_tmp & 0x0700) >> 8
printf "  sels: %x",		($_tmp & 0x0070) >> 4
printf "  selm: %x\n",		($_tmp & 0x0007)

set $_tmp = *(uint16_t *) 0x016a
printf "ucsctl5: %04x\n",	($_tmp)
printf "  divpa: %d",		($_tmp & 0x7000) >> 8 >> 4
printf "  diva: %d",		($_tmp & 0x0700) >> 8
printf "  divs: %d",		($_tmp & 0x0070) >> 4
printf "  divm: %d\n",		($_tmp & 0x0007)  

set $_tmp = *(uint16_t *) 0x016c
printf "ucsctl6: %04x\n",	($_tmp)
printf "  xt2drive: %x",	($_tmp & 0xC000) >> 8 >> 6
printf "  xt2bypass: %x",	($_tmp & 0x1000) >> 8 >> 4
printf "  xt2off: %x\n",	($_tmp & 0x0100) >> 8
printf "  xt1drive: %x",	($_tmp & 0x00C0) >> 6
printf "  xts: %x",  		($_tmp & 0x0020) >> 5
printf "  xt1bypass: %x",	($_tmp & 0x0010) >> 4
printf "  xcap: %x",		($_tmp & 0x000C) >> 2
printf "  smclkoff: %x",	($_tmp & 0x0002) >> 1
printf "  xt1off: %x\n",	($_tmp & 0x0001)  

set $_tmp = *(uint16_t *) 0x016e
printf "ucsctl7: %04x\n",	($_tmp)
printf "  xt2flt: %x",		($_tmp & 0x0008) >> 3
printf "  xt1hfflt: %x",	($_tmp & 0x0004) >> 2
printf "  xt1lfflt: %x",	($_tmp & 0x0002) >> 1
printf "  dcoflt: %x",		($_tmp & 0x0001) 
set $_tmp = *(uint16_t *) 0x102
printf "  oscflt: %x\n",	($_tmp & 0x0002) >> 1

set $_tmp = *(uint16_t *) 0x0170
printf "ucsctl8: %04x\n",	($_tmp)
printf "  modoscreqen: %x",	($_tmp & 0x0008) >> 3
printf "  smclkreqen: %x",	($_tmp & 0x0004) >> 2
printf "  mclkreqen: %x",	($_tmp & 0x0002) >> 1
printf "  aclkreqen: %x\n",	($_tmp & 0x0001)

end
document ucs
display msp430 5xxx unified clock system hardware
end


define dp2
printf "p2  i: %02x  o: %02x  d: %02x  f: %02x  es: %02x  ie: %02x  sel: %02x  ren: %02x  ds: %02x\n",\
    (*(uint8_t *)0x201), (*(uint8_t *)0x203), (*(uint8_t *)0x205), (*(uint8_t *)0x21d), \
    (*(uint8_t *)0x219), (*(uint8_t *)0x21b), (*(uint8_t *)0x20b), (*(uint8_t *)0x207), \
    (*(uint8_t *)0x209)
end
document dp2
display port 2
end

define ports
printf " in  p1: %02x %02x %02x %02x  p5: %02x %02x %02x %02x  p9: %02x %02x %02x\n", \
    (*(uint8_t *)0x200), (*(uint8_t *)0x201), \
    (*(uint8_t *)0x220), (*(uint8_t *)0x221), \
    (*(uint8_t *)0x240), (*(uint8_t *)0x241), \
    (*(uint8_t *)0x260), (*(uint8_t *)0x261), \
    (*(uint8_t *)0x280), (*(uint8_t *)0x281), \
    (*(uint8_t *)0x2a0)

printf "out  p1: %02x %02x %02x %02x  p5: %02x %02x %02x %02x  p9: %02x %02x %02x\n", \
    (*(uint8_t *)0x202), (*(uint8_t *)0x203), \
    (*(uint8_t *)0x222), (*(uint8_t *)0x223), \
    (*(uint8_t *)0x242), (*(uint8_t *)0x243), \
    (*(uint8_t *)0x262), (*(uint8_t *)0x263), \
    (*(uint8_t *)0x282), (*(uint8_t *)0x283), \
    (*(uint8_t *)0x2a2)

printf "dir  p1: %02x %02x %02x %02x  p5: %02x %02x %02x %02x  p9: %02x %02x %02x\n", \
    (*(uint8_t *)0x204), (*(uint8_t *)0x205), \
    (*(uint8_t *)0x224), (*(uint8_t *)0x225), \
    (*(uint8_t *)0x244), (*(uint8_t *)0x245), \
    (*(uint8_t *)0x264), (*(uint8_t *)0x265), \
    (*(uint8_t *)0x284), (*(uint8_t *)0x285), \
    (*(uint8_t *)0x2a4)

printf "sel  p1: %02x %02x %02x %02x  p5: %02x %02x %02x %02x  p9: %02x %02x %02x\n", \
    (*(uint8_t *)0x20a), (*(uint8_t *)0x20b), \
    (*(uint8_t *)0x22a), (*(uint8_t *)0x22b), \
    (*(uint8_t *)0x24a), (*(uint8_t *)0x24b), \
    (*(uint8_t *)0x26a), (*(uint8_t *)0x26b), \
    (*(uint8_t *)0x28a), (*(uint8_t *)0x28b), \
    (*(uint8_t *)0x2aa)

printf "ren  p1: %02x %02x %02x %02x  p5: %02x %02x %02x %02x  p9: %02x %02x %02x\n", \
    (*(uint8_t *)0x206), (*(uint8_t *)0x207), \
    (*(uint8_t *)0x226), (*(uint8_t *)0x227), \
    (*(uint8_t *)0x246), (*(uint8_t *)0x247), \
    (*(uint8_t *)0x266), (*(uint8_t *)0x267), \
    (*(uint8_t *)0x286), (*(uint8_t *)0x287), \
    (*(uint8_t *)0x2a6)

printf " ds: p1: %02x %02x %02x %02x  p5: %02x %02x %02x %02x  p9: %02x %02x %02x\n", \
    (*(uint8_t *)0x208), (*(uint8_t *)0x209), \
    (*(uint8_t *)0x228), (*(uint8_t *)0x229), \
    (*(uint8_t *)0x248), (*(uint8_t *)0x249), \
    (*(uint8_t *)0x268), (*(uint8_t *)0x269), \
    (*(uint8_t *)0x288), (*(uint8_t *)0x289), \
    (*(uint8_t *)0x2a8)

printf "ifg  p1: %02x %02x\n", \
    (*(uint8_t *)0x21c), (*(uint8_t *)0x21d)

printf "ie   p1: %02x %02x    iv:  %04x  %04x\n", \
    (*(uint8_t *)0x21a), (*(uint8_t *)0x21b), (*(uint16_t *) 0x20e), (*(uint16_t *) 0x21e)

printf "ies  p1: %02x %02x\n", \
    (*(uint8_t *)0x218), (*(uint8_t *)0x219)
end
document ports
display h/w ports
end
